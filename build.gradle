// Project description
version = 'dev-alpha'
status = 'indev'
description 'Photon server implementation'

// Java plugin
apply plugin: 'java'
sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
	ext.mainClass = 'org.mcphoton.impl.server.Main'
}

// License plugin
buildscript {
	repositories {
		maven {url 'https://plugins.gradle.org/m2/'}
	}
	dependencies {
		classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
	}
}
apply plugin: 'com.github.hierynomus.license'

license {
	header file('HEADER.txt')
	strictCheck true
	mapping('java', 'SLASHSTAR_STYLE')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	exclude "org/slf4j/impl/Static*.java"
}

// Dependencies
repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	compile project(':Photon-API')
	compile 'net.magik6k:bitbuffer:1.5.1'
	compile 'com.github.TheElectronWill:NBT-javalib:v2.0'
	compile 'com.github.TheElectronWill:JSON-javalib:v1.2.1'
	compile 'com.github.TheElectronWill:TOML-javalib:v1.2.2'
}

// Wrapper config
task wrapper(type: Wrapper) {
	gradleVersion = '2.14.1'
}
