plugins {
	id 'com.github.hierynomus.license' version '0.13.1'
}

// Project description
version = 'dev-alpha'
status = 'indev'
description 'Photon server implementation'

// Java plugin
apply plugin: 'java'
sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// License plugin
apply plugin: 'com.github.hierynomus.license'
license {
	header file('HEADER.txt')
	strictCheck true
	mapping('java', 'SLASHSTAR_STYLE')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	exclude "org/slf4j/impl/Static*.java"
}

// Dependencies
repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url 'https://repo.spacehq.org/content/repositories/releases/'}
	maven { url 'https://repo.spacehq.org/content/repositories/snapshots/' }
}
dependencies {
	compile project(':Photon-API')
	compile 'net.magik6k:bitbuffer:1.5.1'
	compile 'com.github.TheElectronWill:NBT-javalib:v2.0'
	compile 'com.github.TheElectronWill:JSON-javalib:v1.2.1'
	compile 'com.github.TheElectronWill:TOML-javalib:v1.2.2'
}

// Wrapper config
task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}

// Build runnable jar
task fatJar(type: Jar) {
	manifest {
        attributes 'Main-Class': 'org.mcphoton.impl.server.Main'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
